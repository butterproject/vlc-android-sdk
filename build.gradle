buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.github.dcendents.android-maven' version '1.5'
}

apply from: './config/dependencies.gradle'
apply from: './config/vlc.gradle'

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

version = VERSION_NAME
group = GROUP

def localReleaseDestination = "${buildDir}/release/${version}"

repositories {
    mavenCentral()
}

android {
    compileSdkVersion rootProject.ext.compileSdk
    buildToolsVersion rootProject.ext.buildToolsVersion

    publishNonDefault false

    defaultConfig {
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode 1
        versionName VERSION_NAME
    }

    lintOptions {
      abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile "com.android.support:support-v4:${rootProject.ext.supportAnnotationVersion}"
    compile "com.android.support:support-annotations:${rootProject.ext.supportAnnotationVersion}"
}

task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

task zipRelease(type: Zip) {
  from localReleaseDestination
  destinationDir buildDir
  archiveName "release-${version}.zip"
}

artifacts {
  archives androidSourcesJar
}

task generateRelease(dependsOn: ['uploadArchives', 'zipRelease']) {
  doLast {
    println "Release ${version} can be found at ${localReleaseDestination}/"
    println "Release ${version} zipped can be found ${buildDir}/release-${version}.zip"
  }
}

uploadArchives {
    repositories.mavenInstaller {
        pom.project {
            name VERSION_NAME
            description DESCRIPTION
            url URL

            packaging PACKAGING
            groupId GROUP
            artifactId ARTIFACT
            version VERSION_NAME

            licenses {
                license {
                    name LICENSE_NAME
                    url LICENSE_URL
                    distribution 'repo'
                }
            }

            scm {
                connection SCM_URL
                url URL

            }

            developers {
                developer {
                    name DEVELOPER
                }
            }
        }
    }
    repositories.mavenDeployer {
      repository(url: "file://${localReleaseDestination}")
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key =  System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'maven'
        name = ARTIFACT
        userOrg = ORGANIZATION
        licenses = ['GPL-3.0']
        vcsUrl = SCM_URL
        publish = true
        version {
            name = VERSION_NAME
            desc = DESCRIPTION
            released = new Date()
            vcsTag = VERSION_NAME
        }
    }

    filesSpec {
      from 'build/outputs/aar'
      into '.'
    }
}